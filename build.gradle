buildscript {
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE"
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "com.github.spotbugs" version "1.6.2"
}

group = "ninja.javahacker"
version = "0.3.0"
ext.moduleName = "ninja.javahacker.xjfunctions"

ext.versionApiguardian = "1.0.0"
ext.versionCheckstyle = "8.10.1"
ext.versionFindSecBugs = "1.7.1"
ext.versionJcip = "1.0"
ext.versionJunit = "5.2.0"
ext.versionJsr305 = "3.0.2"
ext.versionPmd = "6.4.0"
ext.versionSpotBugs = "3.1.3"

apply plugin: 'base'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'

checkstyle {
    toolVersion = "${versionCheckstyle}"
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    configProperties = [
        "checkstyle.cache.file": "${buildDir}/checkstyle.cache",
    ]
    ignoreFailures = true
    showViolations = true
}

spotbugs {
    toolVersion = "${versionSpotBugs}"
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {

    // Find Security Bugs
    compile group: 'com.h3xstream.findsecbugs', name: 'findsecbugs-plugin', version: "${versionFindSecBugs}"

    // PMD
    compileOnly group: 'net.sourceforge.pmd', name: 'pmd', version: "${versionPmd}"
    testCompileOnly group: 'net.sourceforge.pmd', name: 'pmd', version: "${versionPmd}"

    // SpotBugs
    compileOnly group: 'net.jcip', name: 'jcip-annotations', version: "${versionJcip}"
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: "${versionSpotBugs}"
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: "${versionJsr305}"
    testCompileOnly group: 'net.jcip', name: 'jcip-annotations', version: "${versionJcip}"
    testCompileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: "${versionSpotBugs}"
    testCompileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: "${versionJsr305}"

    // JUnit
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${versionJunit}"
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${versionJunit}"
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "${versionJunit}"
    testCompileOnly group: 'org.apiguardian', name: 'apiguardian-api', version: "${versionApiguardian}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.debug = true
    options.compilerArgs << "-parameters" << "-Xlint:all" << "-Xdoclint:all"
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "xjfunctions"
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

jar {
    duplicatesStrategy = "exclude"
    inputs.property("moduleName", moduleName)
    manifest {
        attributes("Automatic-Module-Name": moduleName)
    }
}

test {
    useJUnitPlatform()
}